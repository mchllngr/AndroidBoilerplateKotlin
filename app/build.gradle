apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.getkeepsafe.dexcount'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.COMPILE_SDK_VERSION
    buildToolsVersion rootProject.ext.BUILD_TOOLS_VERSION

    dexOptions {
        maxProcessCount 4
        javaMaxHeapSize "3g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "de.mchllngr.androidboilerplatekotlin"
        minSdkVersion rootProject.ext.MIN_SDK_VERSION
        targetSdkVersion rootProject.ext.TARGET_SDK_VERSION
        versionCode rootProject.ext.VERSIONCODE
        versionName rootProject.ext.VERSIONAME

        resConfigs "en", "de"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dataBinding.enabled = true

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAliasRelease']
            keyPassword keystoreProperties['keyPasswordRelease']
        }
        debug {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAliasDebug']
            keyPassword keystoreProperties['keyPasswordDebug']
        }
    }

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate true
                optimizeCode true
                proguardFiles getDefaultProguardFile('proguard-defaults.txt'), 'proguard/proguard-rules.pro'
            }
            signingConfig signingConfigs.release
        }

        debug {
            postprocessing {
                removeUnusedCode false
                removeUnusedResources false
                obfuscate false
                optimizeCode false
            }
            signingConfig signingConfigs.debug

            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
            // for merging res-folders together
            res.srcDirs = ['src/main/res']
            file('src/main/res-modules')
                    .listFiles()
                    .each { res.srcDirs += it.path }
        }
        debug.java.srcDirs += 'src/debug/kotlin'
        release.java.srcDirs += 'src/release/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    // App dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "com.android.support:appcompat-v7:$rootProject.ext.SUPPORT_VERSION"
    implementation "com.android.support:design:$rootProject.ext.SUPPORT_VERSION"
    implementation "com.android.support:recyclerview-v7:$rootProject.ext.SUPPORT_VERSION"
    implementation "com.android.support:cardview-v7:$rootProject.ext.SUPPORT_VERSION"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.ext.CONSTRAINT_LAYOUT_VERSION"

    implementation "android.arch.lifecycle:extensions:$rootProject.ext.LIFECYCLE_VERSION"
    implementation "android.arch.lifecycle:common-java8:$rootProject.ext.LIFECYCLE_VERSION"

    implementation "org.koin:koin-android:$rootProject.ext.KOIN_VERSION"
    implementation "org.koin:koin-android-architecture:$rootProject.ext.KOIN_VERSION"

    implementation "com.jakewharton.timber:timber:$rootProject.ext.TIMBER_VERSION"

    implementation "io.reactivex.rxjava2:rxjava:$rootProject.ext.RX_JAVA_VERSION"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.ext.RX_ANDROID_VERSION"

    // Debug-App dependencies
    debugImplementation "com.facebook.stetho:stetho:$rootProject.ext.STETHO_VERSION"

    debugImplementation "io.palaima.debugdrawer:debugdrawer:$rootProject.ext.DEBUGDRAWER_VERSION"
    debugImplementation "io.palaima.debugdrawer:debugdrawer-commons:$rootProject.ext.DEBUGDRAWER_VERSION"
    debugImplementation "io.palaima.debugdrawer:debugdrawer-actions:$rootProject.ext.DEBUGDRAWER_VERSION"
    debugImplementation "io.palaima.debugdrawer:debugdrawer-scalpel:$rootProject.ext.DEBUGDRAWER_VERSION"
    // Used by DebugDrawer
    debugImplementation "com.jakewharton.scalpel:scalpel:$rootProject.ext.SCALPEL_VERSION"

    // Test dependencies
    testImplementation "junit:junit:$rootProject.ext.JUNIT_VERSION"
    testImplementation "org.mockito:mockito-core:$rootProject.ext.MOCKITO_VERSION"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$rootProject.ext.ESPRESSO_VERSION", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
